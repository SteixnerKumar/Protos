
////////////////////////////////////////////////////////////////
// Attacker defns
////////////////////////////////////////////////////////////////


(variables
	(M_COMPROMISED yes no)
	(M_BACKDOORED yes no)
	(M_ROOTED yes no)
	(M_FAKEROOTED yes no)
	(ASSETS_DISCOVERED yes no)
	(DECOYS_DISCOVERED yes no)
	(FILES_PACKED yes no)
	(EXFIL_ONGOING yes no)
)

(observations
	(EXPLOIT_SUCCESS yes no)
	(PERSIST_SUCCESS yes no)
	(SECRETS_FOUND yes no)
	(SENSITIVE_FILE_PACKED yes no)
	(UPLOAD_INIT yes no)
)


////////////////////////////////////////////////////////////////
// Some DD defs for state transitions
////////////////////////////////////////////////////////////////

// compromised state transition under no action
dd compromised_transition
	(M_COMPROMISED
		(yes	(M_COMPROMISED'	(yes	(1.0))
								(no		(0.0))))
		(no		(M_COMPROMISED'	(yes	(0.0))
								(no		(1.0)))))
enddd

// persistance transition under no action
dd backdoored_transition
	(M_BACKDOORED
		(yes	(M_BACKDOORED'	(yes	(1.0))
								(no		(0.0))))
		(no		(M_BACKDOORED'	(yes	(0.0))
								(no		(1.0)))))
enddd

// root transition under no action
dd rooted_transition
	(M_ROOTED
		(yes	(M_ROOTED'	(yes	(1.0))
								(no		(0.0))))
		(no		(M_ROOTED'	(yes	(0.0))
								(no		(1.0)))))
enddd

// fakeroot transition under no action
dd fakerooted_transition
	(M_FAKEROOTED
		(yes	(M_FAKEROOTED'	(yes	(1.0))
								(no		(0.0))))
		(no		(M_FAKEROOTED'	(yes	(0.0))
								(no		(1.0)))))
enddd

// assets_disc transition under no action
dd assets_disc_transition
	(ASSETS_DISCOVERED
		(yes	(ASSETS_DISCOVERED'	(yes	(1.0))
									(no		(0.0))))
		(no		(ASSETS_DISCOVERED'	(yes	(0.0))
									(no		(1.0)))))
enddd

// decoy_disc transition under no action
dd decoys_disc_transition
	(DECOYS_DISCOVERED
		(yes	(DECOYS_DISCOVERED'	(yes	(1.0))
									(no		(0.0))))
		(no		(DECOYS_DISCOVERED'	(yes	(0.0))
									(no		(1.0)))))
enddd

// files_packed transition under no action
dd files_packed_transition
	(FILES_PACKED
		(yes	(FILES_PACKED'	(yes	(1.0))
								(no		(0.0))))
		(no		(FILES_PACKED'	(yes	(0.0))
								(no		(1.0)))))
enddd

// exfil_ongoing transition under no action
dd exfil_ongoing_transition
	(EXFIL_ONGOING
		(yes	(EXFIL_ONGOING'	(yes	(1.0))
								(no		(0.0))))
		(no		(EXFIL_ONGOING'	(yes	(0.0))
								(no		(1.0)))))
enddd

// compromised transition for exploit action
dd compromised_exploit
	(M_COMPROMISED
		(yes	(M_COMPROMISED'	(yes	(1.0))
								(no		(0.0))))
		(no		(M_COMPROMISED'	(yes	(0.9))
								(no		(0.1))))
	)
enddd

// rooted transition for exploit action
dd rooted_exploit
	(M_COMPROMISED
		(yes	(M_ROOTED
					(yes	(M_ROOTED'	(yes	(1.0))
										(no		(0.0))))
					(no		(M_ROOTED'	(yes	(0.5))
										(no		(0.5))))))
		(no		(M_ROOTED
					(yes	(M_ROOTED'	(yes	(0.0))
										(no		(1.0))))
					(no		(M_ROOTED'	(yes	(0.25))
										(no		(0.75))))))
	)
enddd

// backdoored transition for exploit action
dd backdoored_exploit
	(M_BACKDOORED	
		(yes	(M_BACKDOORED'	(yes	(1.0))
								(no		(0.0))))
		(no		(M_BACKDOORED'	(yes	(0.25))
								(no		(0.75))))
	)
enddd

// backdoored transition for persist action
dd backdoored_persist
	(M_COMPROMISED
		(yes	(M_ROOTED	
					(yes	(M_BACKDOORED
								(yes	(M_BACKDOORED'	(yes	(1.0))
														(no		(0.0))))
								(no		(M_BACKDOORED'	(yes	(0.75))
														(no		(0.25))))))
					(no		(M_BACKDOORED
								(yes	(M_BACKDOORED'	(yes	(1.0))
														(no		(0.0))))
								(no		(M_BACKDOORED'	(yes	(0.25))
														(no		(0.75))))))))
		(no		(M_ROOTED	
					(yes	(M_BACKDOORED
								(yes	(M_BACKDOORED'	(yes	(0.0))
														(no		(1.0))))
								(no		(M_BACKDOORED'	(yes	(0.0))
														(no		(1.0))))))
					(no		(M_BACKDOORED
								(yes	(M_BACKDOORED'	(yes	(0.0))
														(no		(1.0))))
								(no		(M_BACKDOORED'	(yes	(0.0))
														(no		(1.0))))))))
	)
enddd

// fakerooted transition for exploit action
dd fakerooted_exploit
	(M_COMPROMISED
		(yes	(M_FAKEROOTED
					(yes	(M_FAKEROOTED'	(yes	(1.0))
											(no		(0.0))))
					(no		(M_FAKEROOTED'	(yes	(0.5))
											(no		(0.5))))))
		(no		(M_FAKEROOTED
					(yes	(M_FAKEROOTED'	(yes	(0.0))
											(no		(1.0))))
					(no		(M_FAKEROOTED'	(yes	(0.25))
											(no		(0.75))))))
	)
enddd

// assets_discovered transition for action file_recon
dd assets_disc_file_recon
	(M_ROOTED
		(yes	(ASSETS_DISCOVERED
					(yes	(ASSETS_DISCOVERED'	(yes	(1.0))
												(no		(0.0))))
					(no		(ASSETS_DISCOVERED'	(yes	(0.75))
												(no		(0.25))))))
		(no		(ASSETS_DISCOVERED
					(yes	(ASSETS_DISCOVERED'	(yes	(1.0))
												(no		(0.0))))
					(no		(ASSETS_DISCOVERED'	(yes	(0.5))
												(no		(0.5))))))
	)
enddd

// decoys_discovered transition for action file_recon
dd decoys_disc_file_recon
	(M_FAKEROOTED
		(yes	(DECOYS_DISCOVERED
					(yes	(DECOYS_DISCOVERED'	(yes	(1.0))
												(no		(0.0))))
					(no		(DECOYS_DISCOVERED'	(yes	(0.75))
												(no		(0.25))))))
		(no		(DECOYS_DISCOVERED
					(yes	(DECOYS_DISCOVERED'	(yes	(1.0))
												(no		(0.0))))
					(no		(DECOYS_DISCOVERED'	(yes	(0.5))
												(no		(0.5))))))
	)
enddd

// files_packed transition for action package
dd files_packed_package
	(ASSETS_DISCOVERED
		(yes	(DECOYS_DISCOVERED	
					(yes	(FILES_PACKED
								(yes	(FILES_PACKED'	(yes	(1.0))
														(no		(0.0))))
								(no		(FILES_PACKED'	(yes	(0.75))
														(no		(0.25))))))
					(no		(FILES_PACKED
								(yes	(FILES_PACKED'	(yes	(1.0))
														(no		(0.0))))
								(no		(FILES_PACKED'	(yes	(0.75))
														(no		(0.25))))))))
		(no		(DECOYS_DISCOVERED	
					(yes	(FILES_PACKED
								(yes	(FILES_PACKED'	(yes	(1.0))
														(no		(0.0))))
								(no		(FILES_PACKED'	(yes	(0.75))
														(no		(0.25))))))
					(no		(FILES_PACKED
								(yes	(FILES_PACKED'	(yes	(0.0))
														(no		(1.0))))
								(no		(FILES_PACKED'	(yes	(0.0))
														(no		(1.0))))))))
	)
enddd

// exfil_ongoing transition for action exfil
dd exfil_ongoing_exfil
	(ASSETS_DISCOVERED
		(yes	(DECOYS_DISCOVERED	
					(yes	(EXFIL_ONGOING
								(yes	(EXFIL_ONGOING'	(yes	(1.0))
														(no		(0.0))))
								(no		(EXFIL_ONGOING'	(yes	(0.75))
														(no		(0.25))))))
					(no		(EXFIL_ONGOING
								(yes	(EXFIL_ONGOING'	(yes	(1.0))
														(no		(0.0))))
								(no		(EXFIL_ONGOING'	(yes	(0.75))
														(no		(0.25))))))))
		(no		(DECOYS_DISCOVERED	
					(yes	(EXFIL_ONGOING
								(yes	(EXFIL_ONGOING'	(yes	(1.0))
														(no		(0.0))))
								(no		(EXFIL_ONGOING'	(yes	(0.75))
														(no		(0.25))))))
					(no		(EXFIL_ONGOING
								(yes	(EXFIL_ONGOING'	(yes	(0.0))
														(no		(1.0))))
								(no		(EXFIL_ONGOING'	(yes	(0.0))
														(no		(1.0))))))))
	)
enddd

////////////////////////////////////////////////////////////////
// DDs for observation model
////////////////////////////////////////////////////////////////

// observation exploit_success
dd exploit_success_exploit
	(M_COMPROMISED'
		(yes	(M_ROOTED'	
					(yes	(M_FAKEROOTED'
								(yes	(EXPLOIT_SUCCESS'	(yes	(0.7))
															(no		(0.3))))
								(no		(EXPLOIT_SUCCESS'	(yes	(0.7))
															(no		(0.3))))))
					(no		(M_FAKEROOTED'
								(yes	(EXPLOIT_SUCCESS'	(yes	(0.7))
															(no		(0.3))))
								(no		(EXPLOIT_SUCCESS'	(yes	(0.7))
															(no		(0.3))))))))
		(no		(M_ROOTED'	
					(yes	(M_FAKEROOTED'
								(yes	(EXPLOIT_SUCCESS'	(yes	(0.7))
															(no		(0.3))))
								(no		(EXPLOIT_SUCCESS'	(yes	(0.7))
															(no		(0.3))))))
					(no		(M_FAKEROOTED'
								(yes	(EXPLOIT_SUCCESS'	(yes	(0.7))
															(no		(0.3))))
								(no		(EXPLOIT_SUCCESS'	(yes	(0.001))
															(no		(0.999))))))))
	)
enddd

// observation persist_success for exploit
dd persist_success_exploit
	(M_BACKDOORED'
		(yes	(PERSIST_SUCCESS'	(yes	(0.7))
									(no		(0.3))))
		(no		(PERSIST_SUCCESS'	(yes	(0.001))
									(no		(0.999))))
	)
enddd

// observation secrets_found for action file_recon
dd secrets_found_file_recon
	(ASSETS_DISCOVERED'
		(yes	(DECOYS_DISCOVERED'
					(yes	(SECRETS_FOUND'		(yes	(1.0))
												(no		(0.0))))
					(no		(SECRETS_FOUND'		(yes	(1.0))
												(no		(0.0))))))
		(no		(DECOYS_DISCOVERED'
					(yes	(SECRETS_FOUND'		(yes	(1.0))
												(no		(0.0))))
					(no		(SECRETS_FOUND'		(yes	(0.0))
												(no		(1.0))))))
	)
enddd

// observation SENSITIVE_FILE_PACKED for action PACKAGE
dd sensitive_file_packed_package
	(FILES_PACKED'
		(yes	(SENSITIVE_FILE_PACKED'		(yes	(1.0))
											(no		(0.0))))
		(no		(SENSITIVE_FILE_PACKED'		(yes	(0.0))
											(no		(1.0))))
	)
enddd

// observation UPLOAD_INIT for action EXFIL
dd upload_init_exfil
	(EXFIL_ONGOING'
		(yes	(UPLOAD_INIT'		(yes	(1.0))
									(no		(0.0))))
		(no		(UPLOAD_INIT'		(yes	(0.0))
									(no		(1.0))))
	)
enddd

////////////////////////////////////////////////////////////////
// define initial_belief
////////////////////////////////////////////////////////////////
init	[*
			(M_COMPROMISED	(yes	(0.0))
							(no		(1.0)))
			(M_BACKDOORED	(yes	(0.0))
							(no		(1.0)))
			(M_ROOTED		(yes	(0.0))
							(no		(1.0)))
			(M_FAKEROOTED	(yes	(0.0))
							(no		(1.0)))
			(ASSETS_DISCOVERED	(yes	(0.0))
								(no		(1.0)))
			(DECOYS_DISCOVERED	(yes	(0.0))
								(no		(1.0)))
			(FILES_PACKED	(yes	(0.0))
							(no		(1.0)))
			(EXFIL_ONGOING	(yes	(0.0))
							(no		(1.0)))
		]
////////////////////////////////////////////////////////////////
// define transitions for actions
////////////////////////////////////////////////////////////////

action NOP
	M_COMPROMISED (compromised_transition)
	M_BACKDOORED (backdoored_transition)
	M_ROOTED (rooted_transition)
	M_FAKEROOTED (fakerooted_transition)
	ASSETS_DISCOVERED (assets_disc_transition)
	DECOYS_DISCOVERED (decoys_disc_transition)
	FILES_PACKED (files_packed_transition)
	EXFIL_ONGOING (exfil_ongoing_transition)
	
	observe
		EXPLOIT_SUCCESS (EXPLOIT_SUCCESS' (yes 	(0.0))	(no	(1.0)))
		PERSIST_SUCCESS (PERSIST_SUCCESS' (yes 	(0.0))	(no	(1.0)))
		SECRETS_FOUND (SECRETS_FOUND' (yes 	(0.0))	(no	(1.0)))
		SENSITIVE_FILE_PACKED (SENSITIVE_FILE_PACKED' (yes 	(0.0))	(no	(1.0)))
		UPLOAD_INIT (UPLOAD_INIT' (yes 	(0.0))	(no	(1.0)))
	endobserve
endaction

action EXPLOIT
	M_COMPROMISED	(compromised_exploit)
	M_BACKDOORED	(backdoored_exploit)
	M_ROOTED		(rooted_exploit)
	M_FAKEROOTED	(fakerooted_exploit)
	ASSETS_DISCOVERED	(assets_disc_transition)
	DECOYS_DISCOVERED	(decoys_disc_transition)
	FILES_PACKED	(files_packed_transition)
	EXFIL_ONGOING	(exfil_ongoing_transition)
	
	observe
		EXPLOIT_SUCCESS	(exploit_success_exploit)
		PERSIST_SUCCESS	(persist_success_exploit)
		SECRETS_FOUND	(SECRETS_FOUND'	(yes	(0.0))	(no		(1.0)))
		SENSITIVE_FILE_PACKED	(SENSITIVE_FILE_PACKED' (yes 	(0.0))	(no	(1.0)))
		UPLOAD_INIT (UPLOAD_INIT' (yes 	(0.0))	(no	(1.0)))
	endobserve
endaction

action PERSIST
	M_COMPROMISED	(compromised_transition)
	M_BACKDOORED	(backdoored_persist)
	M_ROOTED		(rooted_transition)
	M_FAKEROOTED	(fakerooted_transition)
	ASSETS_DISCOVERED	(assets_disc_transition)
	DECOYS_DISCOVERED	(decoys_disc_transition)
	FILES_PACKED	(files_packed_transition)
	EXFIL_ONGOING	(exfil_ongoing_transition)
	
	observe
		EXPLOIT_SUCCESS	(EXPLOIT_SUCCESS'	(yes	(0.0))	(no		(1.0)))
		PERSIST_SUCCESS	(persist_success_exploit)
		SECRETS_FOUND	(SECRETS_FOUND'	(yes	(0.0))	(no		(1.0)))
		SENSITIVE_FILE_PACKED	(SENSITIVE_FILE_PACKED' (yes 	(0.0))	(no	(1.0)))
		UPLOAD_INIT (UPLOAD_INIT' (yes 	(0.0))	(no	(1.0)))
	endobserve
endaction

action FILE_RECON
	M_COMPROMISED (compromised_transition)
	M_BACKDOORED (backdoored_transition)
	M_ROOTED (rooted_transition)
	M_FAKEROOTED (fakerooted_transition)
	ASSETS_DISCOVERED (assets_disc_file_recon)
	DECOYS_DISCOVERED (decoys_disc_file_recon)
	FILES_PACKED (files_packed_transition)
	EXFIL_ONGOING (exfil_ongoing_transition)
	
	observe
		EXPLOIT_SUCCESS (EXPLOIT_SUCCESS' (yes 	(0.0))	(no	(1.0)))
		PERSIST_SUCCESS (PERSIST_SUCCESS' (yes 	(0.0))	(no	(1.0)))
		SECRETS_FOUND (secrets_found_file_recon)
		SENSITIVE_FILE_PACKED (SENSITIVE_FILE_PACKED' (yes 	(0.0))	(no	(1.0)))
		UPLOAD_INIT (UPLOAD_INIT' (yes 	(0.0))	(no	(1.0)))
	endobserve
endaction

action PACKAGE
	M_COMPROMISED (compromised_transition)
	M_BACKDOORED (backdoored_transition)
	M_ROOTED (rooted_transition)
	M_FAKEROOTED (fakerooted_transition)
	ASSETS_DISCOVERED (assets_disc_transition)
	DECOYS_DISCOVERED (decoys_disc_transition)
	FILES_PACKED (files_packed_package)
	EXFIL_ONGOING (exfil_ongoing_transition)
	
	observe
		EXPLOIT_SUCCESS (EXPLOIT_SUCCESS' (yes 	(0.0))	(no	(1.0)))
		PERSIST_SUCCESS (PERSIST_SUCCESS' (yes 	(0.0))	(no	(1.0)))
		SECRETS_FOUND (SECRETS_FOUND' (yes 	(0.0))	(no	(1.0)))
		SENSITIVE_FILE_PACKED (sensitive_file_packed_package)
		UPLOAD_INIT (UPLOAD_INIT' (yes 	(0.0))	(no	(1.0)))
	endobserve
endaction

action EXFIL
	M_COMPROMISED (compromised_transition)
	M_BACKDOORED (backdoored_transition)
	M_ROOTED (rooted_transition)
	M_FAKEROOTED (fakerooted_transition)
	ASSETS_DISCOVERED (assets_disc_transition)
	DECOYS_DISCOVERED (decoys_disc_transition)
	FILES_PACKED (files_packed_transition)
	EXFIL_ONGOING (exfil_ongoing_exfil)
	
	observe
		EXPLOIT_SUCCESS (EXPLOIT_SUCCESS' (yes 	(0.0))	(no	(1.0)))
		PERSIST_SUCCESS (PERSIST_SUCCESS' (yes 	(0.0))	(no	(1.0)))
		SECRETS_FOUND (SECRETS_FOUND' (yes 	(0.0))	(no	(1.0)))
		SENSITIVE_FILE_PACKED (SENSITIVE_FILE_PACKED' (yes 	(0.0))	(no	(1.0)))
		UPLOAD_INIT (upload_init_exfil)
	endobserve
endaction

////////////////////////////////////////////////////////////////
// reward function and all that
////////////////////////////////////////////////////////////////

reward	[+
			(M_COMPROMISED			(yes	(1.0))
									(no		(0.0)))
			(M_ROOTED				(yes	(2.0))
									(no		(0.0)))
			(EXFIL_ONGOING			(yes	(3.0))
									(no		(0.0)))
]

discount 0.9
tolerance 0.0001
